!! --Aim: combine spectral data of the two spectrometers in one sidband of AAEX  SHeFI
!! --written by Zhi-Yu Zhang : pmozhang@gmail.com 
!! -- last change: 15 Sep. 2016
!! -- It only works for SEPIA band-9 with names of 
!!  AP-N601-XF01 AP-N602-XF02  AP-N602-XF03 AP-N602-XF03
!!  601 and 602 are two different polarisations, and (F01,F02), (F03,F04) are two IF pairs 
!! -- usage: @ combine_twoIFs.class  inputfile.apex sourcename time_to_smooth 
!! -- example: @ combine_twoIFs.class E-097.F-9808A-2016-2016-08-18.apex  NA.V1.186  10 
!! -- where 10 is how many times you want to smooth 

set     ext .apex
set     unit v c
set     number *
set     wei eq
pen     /def 
define  char*50  sourcename infile 
define  real tmp sigma1
define  int numbv numbh sm_times /g

begin procedure sm_spec 
    for i 1 to sm_times 
        sm box 2
    next 
end procedure sm_spec


cl
let  infile &1
let  sourcename &2  
let  sm_times  &3


!-------- reorder the scans for pol1 
file   in  'infile'
find
sic  dele 'sourcename'_reorder_pol1.apex
file out  'sourcename'_reorder_pol1.apex m
find   /sour 'sourcename'   /tel AP-N601-XF01 
find a /sour 'sourcename'   /tel AP-N601-XF02 
copy 

!--------- subtract baseline for each IF 
file in  'sourcename'_reorder_pol1.apex 
find 
sic dele 'sourcename'_reorder_pol1_base.apex
file out 'sourcename'_reorder_pol1_base.apex m

for i 1 to found/2 
 get idx%num[i]
   let tmp  channels-(channels-reference)*2
   set win  rx[1] rx[tmp]
   bas 0 
write  i
next 

for i 1 to found/2  
get idx%num['i+found/2']
   let tmp channels-(channels-reference)*2
   set win   rx[1]  rx[tmp] 
   bas 0 
write 'i+found/2'
next


!-------- reorder the scans for pol2 
file   in  'infile'
find
sic  dele 'sourcename'_reorder_pol2.apex
file out  'sourcename'_reorder_pol2.apex m
find   /sour 'sourcename'   /tel AP-N602-XF03 
find a /sour 'sourcename'   /tel AP-N602-XF04 
copy 

!--------- subtract baseline for each IF 
file in  'sourcename'_reorder_pol2.apex 
find 
sic dele 'sourcename'_reorder_pol2_base.apex
file out 'sourcename'_reorder_pol2_base.apex m

for i 1 to found/2 
 get idx%num[i]
   let tmp  channels-(channels-reference)*2
   set win  rx[1] rx[tmp]
   bas 0 
write  i
next 

for i 1 to found/2  
get idx%num['i+found/2']
   let tmp channels-(channels-reference)*2
   set win   rx[1]  rx[tmp] 
   bas 0 
write 'i+found/2'
next



!----------- combine the two IFs
file in  'sourcename'_reorder_pol1_base.apex 
find 
let numbv found 
sic dele 'infile'_'sourcename'_reorder_base_combine.apex 
file out 'infile'_'sourcename'_reorder_base_combine.apex  m

for i 1 to found/2 
    find  /num  'i+numbv/2'  'i+numbv/2'
    find  a /num   i           i 
    
    !--------------------
    cl
    set mod x -1000 1000
    set mod y -0.2 0.2 
    !--------------------
    
    ! -------- plot IF-1
    
    get f 
    @sm_spec sm_times  
    spec /pen 1
    box
    
    ! -------- plot IF-2
    
    get n 
    @sm_spec sm_times  
    spec /pen 3 
    
    ! ------- plot the combined spectrum
    
    set  align f c 
    ave /re
    write i 
    @sm_spec sm_times  
    spec /pen 0
    
    sic wait 0.3 
next 

file in  'sourcename'_reorder_pol2_base.apex 
find 
let numbh found 

for i 1 to found/2 
    find  /num  'i+numbh/2'  'i+numbh/2'
    find  a /num   i           i 
    
    !--------------------
    cl
    set mod x -1000 1000
    set mod y -0.2 0.2 
    !--------------------
    
    ! -------- plot IF-1
    
    get f 
    @sm_spec sm_times  
    spec /pen 1
    box
    
    ! -------- plot IF-2
    
    get n 
    @sm_spec sm_times  
    spec /pen 3 
    
    ! ------- plot the combined spectrum
    
    set  align f c 
    ave /re
    write 'i + numbv'
    @sm_spec sm_times  
    spec /pen 0
    
    sic wait 0.3  
next 



sic dele 'sourcename'_reorder_pol1_base.apex 
sic dele 'sourcename'_reorder_pol1.apex 
sic dele 'sourcename'_reorder_pol2_base.apex 
sic dele 'sourcename'_reorder_pol2.apex 

del /var numbv numbh sm_times

